<?xml version="1.0" encoding="UTF-8"?>
<ValidationRule xmlns="http://soap.sforce.com/2006/04/metadata">
    <fullName>CheckEditWhenStatusClosedOrCompleted</fullName>
    <active>true</active>
    <description>Prevents Edit of record when Order Status is Completed/Closed. Bypass validation when set from lightning component. Allows system admin to allow status change</description>
    <errorConditionFormula>AND(
NOT($Profile.Name ==$Label.Integration_SAP_Service),
NOT($Profile.Name == $Label.Integration_Service_SNAP_Profile),
NOT($User.FirstName ==$Label.Automated_Process_User), 
NOT($Setup.cs_Apex_Settings__c.Bypass_All_Validations__c),

NOT(ISNEW()),
TEXT(SVMXC__Order_Status__c) != $Label.WorkOrderStatusReopenRequested,
OR(
TEXT(PRIORVALUE(SVMXC__Order_Status__c))== $Label.WorkOrderStatusCompleted,
TEXT(PRIORVALUE(SVMXC__Order_Status__c))== $Label.WorkOrderStatusClosed,
TEXT(PRIORVALUE(SVMXC__Order_Status__c))== &apos;Closed-Duplicate&apos;
),
NOT(Record_Type__c = $Label.WorkOrderRecordTypeFieldStockMgmt),
NOT(Service_Report_Generation__c = true),
NOT(ISCHANGED(Is_Reopen_Approved__c)),
NOT(ISCHANGED(Is_Service_Report_Generated__c)),
NOT(ISCHANGED(Qualified_for_Batched_Sync_to_ERP__c)),/*DCP-37747*/
NOT(ISCHANGED(Warning_Message__c)),/*DCP-37747*/
NOT(ISCHANGED(Last_Service_Report_Sent__c )),
NOT(ISCHANGED(Is_Customer_Sign_Captured__c)),
NOT(ISCHANGED(Parts_Order_Not_Requested__c)),
NOT(ISCHANGED(Has_Verification_Code__c)),
NOT(ISCHANGED(Contact_Preferred_Language__c)),
NOT(ISCHANGED(Signature_Type__c)),
NOT(ISCHANGED(Is_Set_From_Lightning_Component__c)),
NOT(ISCHANGED(Total_Open_QS_Review_activity__c)),
NOT(ISCHANGED(Total_CS_Review_Activity_Open__c)),
NOT(ISCHANGED(Total_Open_Work_Details__c)),
NOT(ISCHANGED(SVMXC__Total_Billable_Amount__c)),
NOT(ISCHANGED(Total_Open_IQ_OQ_or_OQ_or_PQ_activity__c)),
NOT(ISCHANGED(SVMXC__Total_Work_Order_Cost__c)),


NOT(
OR(

/*Scenario 1: Bypass for Account Maintenance and Presales type Work Orders*/
AND(
OR(
Completed_When_Created__c,
ISCHANGED(Work_Detail_Count__c)
),
RecordType.Name = $Label.WorkOrderRecordTypeServiceSupport,
OR(
TEXT(SVMXC__Order_Type__c)=$Label.WorkOrderTypeAccountMaintenance,
TEXT(SVMXC__Order_Type__c)=$Label.WorkOrderTypePresales
)
),

/*Scenario 2: Bypass conditions on WO status = &quot;Completed&quot; */
AND(
TEXT(PRIORVALUE(SVMXC__Order_Status__c))== $Label.WorkOrderStatusCompleted,
OR(
TEXT(SVMXC__Order_Status__c)== $Label.WorkOrderStatusClosed,
ISCHANGED(SVMX_PS_Number_of_Open_Usage_Lines__c),
ISCHANGED(Parts_Order_Status__c),
ISCHANGED(Total_Field_Compliance_Reviews_Open__c),

$Profile.Name ==$Label.ProfileFieldCompliance,
$Profile.Name ==$Label.ProfileSystemAdmin,
$Permission.SS_Field_Compliance_Review

)
),

/*Scenario 3: Checking Conditions for Reopening WO on cases */
AND(
AND(
NOT(ISNUll(SVMXC__Case__c)),
NOT(
OR(
SVMXC__Case__r.IsClosed,
TEXT(SVMXC__Case__r.Status)== $Label.CaseStatusCompleted,
TEXT(SVMXC__Case__r.Status)== $Label.CaseStatusPendingLogisticsReview,
TEXT(SVMXC__Case__r.Status)== $Label.CaseStatusPendingQAReview,
TEXT(SVMXC__Case__r.Status)== $Label.CaseStatusReopenRequested
))),
OR(
TEXT(SVMXC__Order_Status__c) == $Label.WorkOrderStatusAssigned,
TEXT(SVMXC__Order_Status__c) == $Label.WorkOrderStatusInProgress,
TEXT(SVMXC__Order_Status__c) == $Label.WorkOrderStatusTechnicianOnsite,

/* reopen work order from work order by pass */
AND(
RecordType.Name == $Label.WorkOrderRecordTypeRemoteSupport,
TEXT(SVMXC__Order_Status__c)==$Label.WorkOrderStatusInProgress
),
/* reopen work order from work order by pass for cp */
AND(
$Setup.Channel_Partner_Profile_Settings__c.Is_Channel_Partner__c,
TEXT(SVMXC__Order_Status__c)==$Label.WorkOrderStatusAssignmentAccepted
)
)
),

/* Scneraio 4 */
AND(
TEXT(SVMXC__Order_Status__c)== $Label.WorkOrderStatusCompleted,
ISCHANGED(Number_of_Canceled_Work_Details__c)
)
)
)
)</errorConditionFormula>
    <errorMessage>Work Order cannot be edited when status is Completed/Closed</errorMessage>
</ValidationRule>
