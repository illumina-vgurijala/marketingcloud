<?xml version="1.0" encoding="UTF-8"?>
<ValidationRule xmlns="http://soap.sforce.com/2006/04/metadata">
    <fullName>CheckCaseAssociatedToWO</fullName>
    <active>true</active>
    <description>Check Analysis Code and Resolution Code if WO is associated to Business Issue case</description>
    <errorConditionFormula>AND(
NOT($Profile.Name ==$Label.Integration_SAP_Service),
NOT($Profile.Name == $Label.Integration_Service_SNAP_Profile),
NOT($User.FirstName ==$Label.Automated_Process_User),
NOT($Setup.cs_Apex_Settings__c.Bypass_All_Validations__c),

OR(
AND(
SVMXC__Case__r.RecordType.Name =$Label.CaseRecordTypeBusinessIssue,
ISBLANK(TEXT(Abandoned_Reason__c)),
OR(TEXT(SVMXC__Case__r.Sub_Type__c) = $Label.CaseSubTypeComplaint,
TEXT(SVMXC__Case__r.Sub_Type__c) = $Label.CaseSubTypeGeneralIssue
),
OR(
TEXT(SVMXC__Order_Status__c)= $Label.WorkOrderStatusCompleted,
TEXT(SVMXC__Order_Status__c)= $Label.WorkOrderStatusClosed
),
OR(AND (
OR(AND (NOT(TEXT(SVMXC__Case__r.Category__c)=$Label.CaseCategoryLogistics),NOT(SVMXC__Case__r.Has_Analysis_Code__c)),NOT(SVMXC__Case__r.Has_Resolution_Code__c)),
OR(TEXT(SVMXC__Case__r.Sub_Type__c) = $Label.CaseSubTypeComplaint,
TEXT(SVMXC__Case__r.Sub_Type__c) = $Label.CaseSubTypeGeneralIssue
)
),
AND(
NOT(SVMXC__Case__r.HasHazardCode__c),
TEXT(SVMXC__Case__r.Sub_Type__c) =$Label.CaseSubTypeComplaint,
OR(
SVMXC__Case__r.IVD_Product_Identifier_Additional_Prod__c = true,
SVMXC__Case__r.IVD_Product_Identifier__c = true,
ISPICKVAL(SVMXC__Case__r.PAE_PFA_Evaluation_Required__c , &apos;Yes&apos;)
)
)
)
),

/* New Condition for Service Request Case CMCM-2392 */
AND(
$CustomMetadata.Feature_Flag__mdt.IVD_Workflow.IsOn__c,
ISBLANK(TEXT(Abandoned_Reason__c)),
SVMXC__Case__r.RecordType.Name = $Label.CaseRecordTypeServiceRequest,
OR(
TEXT(SVMXC__Order_Status__c) = $Label.WorkOrderStatusCompleted,
TEXT(SVMXC__Order_Status__c) = $Label.WorkOrderStatusClosed
),
OR(
AND(
RecordType.Name = $Label.WorkOrderRecordTypeFieldService,
OR(
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeServiceActivities,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeRelocation,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeInstallation,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypePreventativeMaintenance,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeBillableInstall
)
),
AND(
    RecordType.Name = $Label.WorkOrderRecordTypeServiceSupport,
    TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeProfessionalServices
)
),
NOT(SVMXC__Case__r.Has_Resolution_Code__c)
),
/* New Condition for CMCM-10145 */
AND(
$CustomMetadata.Feature_Flag__mdt.IVD_Workflow.IsOn__c,
OR(
TEXT(SVMXC__Order_Status__c) = $Label.WorkOrderStatusCompleted,
TEXT(SVMXC__Order_Status__c) = $Label.WorkOrderStatusClosed
),
OR(
AND(RecordType.Name = $Label.WorkOrderRecordTypeFieldService,
OR(
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypePreventativeMaintenance,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeInstallation,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeBillableInstall
)),
AND(
RecordType.Name = $Label.WorkOrderRecordTypeServiceSupport,
TEXT(SVMXC__Order_Type__c) = $Label.WorkOrderTypeProfessionalServices
)
),
ISBLANK(SVMXC__Case__c),
NOT(Has_Resolution_Code__c)
)
)
)</errorConditionFormula>
    <errorMessage>Record cannot be closed or completes if associated case doesn&apos;t have analysis code, Hazard Code and resolution code</errorMessage>
</ValidationRule>
